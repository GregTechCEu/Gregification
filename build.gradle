buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = mc_version + "-" + gregification_version
group = "gregification"
archivesBaseName = "Gregification"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    useDepAts = true
    makeObfSourceJar = false
    replace("@VERSION@", gregification_version)
    replaceIn("GFValues.java")
}

repositories {
    maven {
        name "ChickenBones' Mods"
        url "http://maven.covers1624.net"
    }
    maven {
        name "CurseForge"
        url "https://www.cursemaven.com"
    }
    maven {
        "JEI"
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        "CraftTweaker"
        url "https://maven.blamejared.com"
    }
    maven {
        name "Forestry"
        url "http://maven.ic2.player.to"
    }
    maven {
        name "CTM"
        url "https://maven.tterrag.com/"
    }
    maven {
        name "OpenComputers"
        url "https://maven.cil.li/"
    }
    maven {
        name "McJty's Mods'"
        url "http://maven.k-4u.nl"
    }
    maven {
        name "Forgelin"
        url "http://maven.shadowfacts.net/"
    }
}

dependencies {

    // Hard-Deps
    deobfCompile "curse.maven:mixin-booter-419286:3321174"
    compile files("gregtech-1.12.2-2.1.1-beta.jar")

    // Mystical Agriculture mods
    deobfCompile "curse.maven:cucumber-272335:2645867"
    compile files("MysticalAgriculture-1.12.2-1.7.5.jar")
    compile files("MysticalAgradditions-1.12.2-1.3.2.jar")

    // CCLib
    deobfCompile "curse.maven:codechicken-lib-1-8-242818:2779848"

    // JEI
    deobfCompile "curse.maven:jei-238222:3040523"

    // McJty's Mods
    deobfCompile "curse.maven:mcjtylib-233105:2745846"
    deobfCompile "curse.maven:the-one-probe-245211:2667280"
    deobfCompile "curse.maven:xnet-260912:2745852"

    // CT
    compile files("CraftTweaker2-1.12-4.1.20.656.jar")

    // Chisel
    deobfCompile "curse.maven:ctm-267602:2915363"

    // Forestry and addons
    deobfCompile "curse.maven:forestry-59751:2918418"
    deobfCompile "curse.maven:binnies-mods-223525:2916129"

    // TiCon
    deobfCompile "curse.maven:tinkers-construct-74072:2902483"
    deobfCompile "curse.maven:mantle-74924:2713386"

    // ExNihilo
    deobfCompile "curse.maven:shadowfacts-forgelin-248453:2785465"
    deobfCompile "curse.maven:ex-nihilo-creatio-274456:2817545"

    // OC
    deobfCompile "curse.maven:opencomputers-223008:2828357"

    // TFC
    deobfCompile "curse.maven:tfcraft-302973:3268988"

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.gregification.refmap.json"
    }
}
